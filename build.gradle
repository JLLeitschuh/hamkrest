buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

plugins {
    id "org.jetbrains.kotlin.multiplatform" version "1.3.21"
    id "com.moowork.node" version "1.2.0"
    id "maven-publish"
}

repositories {
    mavenCentral()
}

group = "com.natpryce"
version(hasProperty("-version") ? property("-version") : "SNAPSHOT")

println "building version ${version}"


kotlin {
    jvm {
        compilations.all {
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }

    js {
        compilations.main {
            kotlinOptions {
                sourceMap = true
                metaInfo = true
                moduleKind = "umd"
            }
        }
        compilations.test {
            kotlinOptions {
                sourceMap = true
                metaInfo = true
                moduleKind = "commonjs"
            }
        }
    }

    macosX64()

    linuxX64()

    sourceSets {
        commonMain {
            dependencies {
                api kotlin('stdlib-common')
            }
        }

        commonTest {
            dependencies {
                api kotlin('test-common')
                api kotlin('test-annotations-common')
            }
        }

        jvmMain {
            dependencies {
                compileOnly kotlin('stdlib-jdk8')
                compileOnly kotlin('reflect')
            }
        }

        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
                implementation kotlin('reflect')
            }
        }


        jsMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-js"
            }
        }

        jsTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-js"
            }
        }

        all {
            languageSettings {
                languageVersion = '1.3'
                apiVersion = '1.3'
            }
        }
    }
}

wrapper {
    gradleVersion = '5.1.1'
}

apply from: "build-jvm.gradle"
apply from: "build-js.gradle"
