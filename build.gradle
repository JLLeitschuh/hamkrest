
plugins {
    id "org.jetbrains.kotlin.multiplatform" version "1.3.20"
    id "maven-publish"
}

repositories {
    mavenCentral()
}

group = "com.natpryce"
version(hasProperty("-version") ? property("-version") : "SNAPSHOT")

println "building version ${version}"


kotlin {
    jvm {
        compilations.all {
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                compileOnly kotlin('stdlib-common')
            }
        }

        commonTest {
            dependencies {
                api kotlin('test-common')
                api kotlin('test-annotations-common')
            }
        }

        jvmMain {
            dependencies {
                compileOnly kotlin('stdlib-jdk8')
                compileOnly kotlin('reflect')
            }
        }

        jvmTest {
            dependencies {
                api kotlin('test-junit')
                api kotlin('reflect')
            }
        }

        all {
            languageSettings {
                languageVersion = '1.3'
                apiVersion = '1.3'
            }
        }
    }
}

jvmTest {
    beforeTest { desc ->
        print "${desc.className.substring("com.natpryce.hamkrest.".length())}: ${desc.name.replace("_", " ")}"
    }
    afterTest { desc, result ->
        println " -> ${result.resultType}"
    }
}


wrapper {
    gradleVersion = '5.1.1'
}

